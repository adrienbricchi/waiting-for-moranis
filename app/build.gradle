/*
 * Waiting For Moranis
 * Copyright (C) 2020-2021
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.application'
    id 'com.google.android.gms.oss-licenses-plugin'
}

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {

    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "org.adrienbricchi.waitingformoranis"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "develop"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "TMDB_KEY", '"' + apikeyProperties['TMDB_KEY'] + '"'
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        // dataBinding true
        viewBinding true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def lombok_version = '1.18.20'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombok_version
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok_version

    def room_version = '1.1.1'
    implementation group: 'android.arch.lifecycle', name: 'extensions', version: room_version
    implementation group: 'android.arch.persistence.room', name: 'runtime', version: room_version
    annotationProcessor group: 'android.arch.lifecycle', name: 'common-java8', version: room_version
    annotationProcessor group: 'android.arch.persistence.room', name: 'compiler', version: room_version

    def jackson_version = '2.12.4'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jackson_version
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jackson_version
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jackson_version

    implementation group: 'com.android.volley', name: 'volley', version: '1.2.1'
    implementation group: 'com.squareup.picasso', name: 'picasso', version: '2.71828'
    implementation group: 'com.google.android.gms', name: 'play-services-oss-licenses', version: '17.0.0'

    def androidx_navigation_version = '2.3.5'
    implementation group: 'androidx.navigation', name: 'navigation-fragment', version: androidx_navigation_version
    implementation group: 'androidx.navigation', name: 'navigation-ui', version: androidx_navigation_version

    implementation group: 'androidx.fragment', name: 'fragment', version: '1.3.6'
    implementation group: 'androidx.appcompat', name: 'appcompat', version: '1.3.1'
    implementation group: 'androidx.coordinatorlayout', name: 'coordinatorlayout', version: '1.1.0'
    implementation group: 'androidx.recyclerview', name: 'recyclerview', version: '1.2.1'
    implementation group: 'androidx.recyclerview', name: 'recyclerview-selection', version: '1.1.0'
    implementation group: 'com.google.android.material', name: 'material', version: '1.5.0-alpha02'
    implementation group: 'androidx.viewpager2', name: 'viewpager2', version: '1.1.0-beta01'
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: '2.1.0'
    implementation group: 'androidx.swiperefreshlayout', name: 'swiperefreshlayout', version: '1.1.0'
    implementation group: 'androidx.preference', name: 'preference', version: '1.1.1'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    androidTestImplementation group: 'androidx.test.ext', name: 'junit', version: '1.1.3'
    androidTestImplementation group: 'androidx.test.espresso', name: 'espresso-core', version: '3.4.0'
}
